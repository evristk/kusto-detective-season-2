//--------------------------------
// Case 1: To bill or not to bill?
//--------------------------------

// The data required for this case:
.execute database script <|
// The script takes ~20seconds to complete ingesting all the data.
.set-or-replace Costs <| 
    datatable(MeterType:string, Unit:string, Cost:double) [
     'Water', 'Liter', 0.001562, 
     'Electricity', 'kwH', 0.3016]
.create-merge table Consumption (Timestamp:datetime , HouseholdId:string, MeterType:string, Consumed:double)
.ingest async into table Consumption (@'https://kustodetectiveagency.blob.core.windows.net/kda2c1taxbills/log_00000.csv.gz')
.ingest async into table Consumption (@'https://kustodetectiveagency.blob.core.windows.net/kda2c1taxbills/log_00001.csv.gz')
.ingest into table Consumption (@'https://kustodetectiveagency.blob.core.windows.net/kda2c1taxbills/log_00002.csv.gz')



//----------------------------------
// Goal: Identify anomalies in Consumption data, fix them and calculate an accurate amount to be paid
//----------------------------------

//----------------------------------
// Solution 
//----------------------------------

Consumption
| extend Consumed=abs(Consumed)
| distinct *
| summarize sum(Consumed) by MeterType
| join kind=inner Costs on MeterType
| summarize sum(sum_Consumed*Cost)


//----------------------------------
// Solution with detailed breakdowns
//----------------------------------

Consumption
// Some readings had negative values, we fixed those by taking abs() of the consumption.
| extend Consumed=abs(Consumed)
// After converting negative values to positive, we have duplicates. We remove those with distinct
| distinct *
// We calculate total consumption for each meter type
| summarize sum(Consumed) by MeterType
// The Costs table has the price per unit for each meter type. We join it with total consumption
| join kind=inner Costs on MeterType
// The result at this point is two rows with total consumption and 
// the price per unit for the corresponding meter type. Summing up 
// the products of total consumption and unit costs gives us the total bill. 
| summarize sum(sum_Consumed*Cost)
